/***********************************************************************************************
Tarea 10 - Colas

Kevin Fallas Alvarado 2019057752
Esteban Madrigal Marin 2018154123
************************************************************************************************/

/**************************     VARIABLES GLOBALES    ***************************************************/
'INPUT emplados
NAME Inicio Salida Entrada Fin x
STRING "" vacio
prom_salida = 0
prom_entrada = 40
tiempo_maximo = 480
prom_salidas = (37 35 33 31)
GLOBAL Inicio Salida Entrada Fin prom_salida prom_entrada x prom_salidas tiempo_maximo vacio

/**************************     MOSTRAR DATOS    ***************************************************/
NEWCMD SHOW_COLA cola
   OUTPUT "("
   FOREACH tiempo cola
      OUTPUT "%0.0f " tiempo
   END
   OUTPUT ") "
END

NEWCMD SHOW_TABLE tiempo evento tiempo_sig_salida tiempo_sig_entrada cola tiempo_ocioso tiempo_espera
   OUTPUT "%4.2f\t\t%8.2f\t\t%8.2f\t\t%8.2f\t\t%8.2f\t\t%8.2f\t\t" tiempo evento tiempo_sig_salida tiempo_sig_entrada tiempo_ocioso tiempo_espera
   SHOW_COLA cola
   PRINT
END

/**************************     SIMULACION    ***************************************************/
NEWCMD SET_SIG_EVENTO tiempo_sig_salida tiempo_sig_entrada tiempo evento
   IF tiempo_sig_salida > tiempo_sig_entrada OR tiempo_sig_salida = x  'VA LA ENTRADA
      evento = Entrada
      tiempo = tiempo_sig_entrada
   ELSE				     			'VA LA SALIDA
      evento = Salida
      tiempo = tiempo_sig_salida
   END
END

NEWCMD ACTUALIZAR_COLA cola tiempo_espera
   index = 1
   FOREACH tiempo cola
      tiempo_nuevo = tiempo + tiempo_espera
      PUT tiempo_nuevo index cola
      index = index + 1
   END
END

newcmd OBTENER_TIEMPO tiempo_actual promedio tiempo_evento_
   exponential 1 promedio tiempo_evento_
   integer ceiling tiempo_evento_ tiempo_evento_ 'Para que no hayan tiempos negativos
   tiempo_evento_ = tiempo_evento_ +  tiempo_actual
end

NEWCMD OBETENER_MEDIA_TIEMPO cantidad_empleados
   index = cantidad_empleados - 2
   TAKE prom_salidas index prom_salida
END

NEWCMD OBTENER_CAMIONES_ESPERA cantidad_camiones_
   UNIFORM 1 0 1 r
   IF r < 0.10
      cantidad_camiones_ = 3
   ELSEIF r < 0.25
      cantidad_camiones_ = 2
   ELSEIF r < 0.50
      cantidad_camiones_ = 1
   ELSE
      cantidad_camiones_ = 0
   END
END

NEWCMD ENCOLAR_CAMIONES cantidad_camiones cola
   REPEAT cantidad_camiones
      SCORE 0 cola
   END
END

NEWCMD DESENCOLAR_CAMIONES cola tiempo tiempo_anterior evento tiempo_sig_entrada tiempo_ocioso
   SIZE cola en_espera
   IF en_espera > 0
      REPEAT en_espera
         TAKE cola 1 tiempo_duracion				' obtenemos la duracion del camion
         tiempo_entrada = tiempo_anterior - tiempo_duracion		' tiempo en el que entro el camion
         REMOVE cola 1 cola 					' lo sacamos de la cola
         tiempo_espera = tiempo - tiempo_entrada			' sacamos cuanto lleva esperando
         OBTENER_TIEMPO tiempo prom_salida tiempo_sig_salida 		' obtenemos el tiempo de salida
         'en_servicio = 1 					' lo ponemos en servicio
         SHOW_TABLE tiempo evento tiempo_sig_salida tiempo_sig_entrada cola tiempo_ocioso tiempo_espera
         tiempo_anterior = tiempo
         tiempo = tiempo_sig_salida
      END
   END
   OUTPUT "%4.2f\t\t" tiempo Fin
END

NEWCMD SIMULAR
   PRINT "TIEMPO\t\tEVENTO\t\tSIG_SAL\t\tSIG_ENT\t\tOCIOSO\t\tESPERA\t\tCOLA"
   tiempo_sig_entrada = 0
   tiempo_sig_salida = x
   tiempo_ocioso = 0
   tiempo_espera = 0
   tiempo = 0
   tiempo_anterior = 0
   evento = Inicio
   cola = ()
   en_servicio = 0
   OBTENER_CAMIONES_ESPERA cantidad_camiones
   ENCOLAR_CAMIONES cantidad_camiones cola
   WHILE tiempo_sig_entrada < tiempo_maximo
      IF evento = Inicio					'===SI INICIA=================================================
         SIZE cola en_espera
         IF en_espera > 0					' si hay alguien en espera
            REMOVE cola 1 cola					' lo sacamos de la cola
            en_servicio = 1					' lo ponemos en servicio
            OBTENER_TIEMPO tiempo prom_salida tiempo_sig_salida 	' obtenemos el tiempo de salida
         END
         OBTENER_TIEMPO tiempo prom_entrada tiempo_sig_entrada 	' obtenemos el siguiente tiempo de entrada
      ELSEIF evento = Entrada                                           '===SI ENTRA UN CAMION=========================================
         IF en_servicio = 1 					' si hay uno en servicio
            tiempo_espera_cola = tiempo - tiempo_anterior		' sacamos el tiempo que lleva en la cola desde el ultimo tiempo
            ACTUALIZAR_COLA cola tiempo_espera_cola			' actualizamos colas
            SCORE 0 cola 					' lo encolo
         ELSE 						' si no hay tiempo de salida
            tiempo_ocioso = tiempo - tiempo_anterior		' sacamos el tiempo que llevan sin trabajar
            en_servicio = 1 					' es porque no hay nadie esperando, entonces metemos el servicio
            OBTENER_TIEMPO tiempo prom_salida tiempo_sig_salida 	' obtenemos el tiempo de salida
         END
         OBTENER_TIEMPO tiempo prom_entrada tiempo_sig_entrada 	' obtenemos el siguiente tiempo de entrada
      ELSEIF evento = Salida	 				'===SI SALE UN CAMION=================================================
         SIZE cola en_espera
         IF en_espera = 0 					' si no hay nadie esperando entonces
            en_servicio = 0 					' no hay nadie en servicio
            tiempo_sig_salida = x 				' no se requiere salida
         ELSE 						' si hay camion esperando
            TAKE cola 1 tiempo_duracion				' obtenemos la duracion del camion
            tiempo_entrada = tiempo_anterior - tiempo_duracion	' tiempo en el que entro el camion
            REMOVE cola 1 cola 				' lo sacamos de la cola
            tiempo_espera = tiempo - tiempo_entrada			' sacamos cuanto lleva esperando
            OBTENER_TIEMPO tiempo prom_salida tiempo_sig_salida 	' obtenemos el tiempo de salida
            en_servicio = 1 					' lo ponemos en servicio
         END
         tiempo_espera_cola = tiempo - tiempo_anterior		' sacamos el tiempo que lleva en la cola desde el ultimo tiempo
         ACTUALIZAR_COLA cola tiempo_espera_cola			' actualizamos colas
      END
      SHOW_TABLE tiempo evento tiempo_sig_salida tiempo_sig_entrada cola tiempo_ocioso tiempo_espera
      tiempo_anterior = tiempo
      tiempo_ocioso = 0
      tiempo_espera = 0
      SET_SIG_EVENTO tiempo_sig_salida tiempo_sig_entrada tiempo evento
   END
   PRINT "================= TIEMPO EXCEDIDO: DESENCOLANDO CAMIONES ===================================================================="
   DESENCOLAR_CAMIONES cola tiempo tiempo_anterior evento tiempo_sig_entrada tiempo_ocioso
END
/***** MAIN *****/
CLEAROUTPUT
'INPUT "Digite la cantidad de los empleados(3, 4, 5, 6): " cantidad_empleados
cantidad_empleados = 3
OBETENER_MEDIA_TIEMPO cantidad_empleados
SIMULAR
