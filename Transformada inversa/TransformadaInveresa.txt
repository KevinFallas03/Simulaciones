/***********************************************************************************************
Tarea 7 - Transformada inversa

Kevin Fallas Alvarado 2019057752
Esteban Madrigal Marin 2018154123

Funcion inicial
         {    0                     si x < 0
f(x)  =  {    (3/2)*(x² - 2*x + 1)  si 0 <= x < 1
         {    x/3                   si 1 <= x < 2 

Funcion acumulada
         {    0                            si x < 0
F(x)  =  {    (x^3)/2 - 3*(x^2)/2 + 3*x/2  si 0 <= x < 1
         {    ((x^2)-1)/6 + 1/2            si 1 <= x < 2 
         {    1                            si  x > 2

Funcion inversa acumulada
x  =     {    1-(1-2r)^(1/3)  si 0 ? r < 1/2
         {    sqrt(6*x-2)     si 1/2 <= r < 1

************************************************************************************************/

CLEAROUTPUT

/*Genera la muestra aleatoria con la transformada inversa de f(x)*/
NEWCMD GENERAR_X  x_
   UNIFORM 1 0 1 r
   IF r >= 0 AND r < 0.5
      base = (1 - 2*r)
      res = base ^ (1/3)
      IF base < 0 'Si la base es negativa
         res = -((ABS(base)) ^ (1/3)) 'Se saca la raiz con el valor positivo y se pone negativo el resultado
      END
      x_ = 1 - res
   ELSE
      x_ = SQRT(2) * SQRT(ABS( 3 * r - 1))
   END
END

NEWCMD OBTENER_MUESTRA_TRASFORMADA_INVERSA cantidad_datos_muestra muestra_
   muestra_ = ()
   REPEAT cantidad_datos_muestra
      GENERAR_X x
      SCORE x muestra_
   END
END

/***** MAIN *****/

/*Lee cantidad de datos a generar: "muestra"*/
INPUT cantidad_datos_muestra

/*Lee los invervalos para el histograma: "n"*/
INPUT intervalos_histograma
rango = 2/intervalos_histograma

/*Genera datos utilizando transformada inversa de f(x)*/
OBTENER_MUESTRA_TRASFORMADA_INVERSA cantidad_datos_muestra muestra

/*Mostrar el histograma*/
HISTOGRAM density binsize rango muestra
