/***********************************************************************************************
Tarea 9 - Inventarios

Kevin Fallas Alvarado 2019057752
Esteban Madrigal Marin 2018154123
************************************************************************************************/

/**************************     COSTOS    ***************************************************/
NEWCMD CALCULAR_COSTO_ORDEN total_ordenes costo_
   costo_ = 100 * total_ordenes
END

NEWCMD ACUMULAR_COSTO_INVENTARIO total_costo_inventario inventario
   IF inventario > 0
      total_costo_inventario = total_costo_inventario + inventario*20
   END
END

NEWCMD ACUMULAR_COSTO_FALTANTES total_costo_faltantes inventario
   IF inventario < 0
      total_costo_faltantes = total_costo_faltantes + ABS(inventario)*50
   END
END

/**************************     ORDENAR    ***************************************************/
NEWCMD REORDENAR bandera_orden dias_orden total_ordenes_ llego_pedido_
   llego_pedido_ = 0
   IF bandera_orden = 1 'hay una orden
      dias_orden = dias_orden - 1
      IF dias_orden = 0 'ya la orden debio llegar
         llego_pedido_ = 1 'indico que llego
         bandera_orden = 0 'entonces ya no hay orden
      END
   ELSE 'generar una orden con el tiempo de entrega respectivo
      bandera_orden = 1 'pido la orden
      dias_orden = 3 'REVISAR
      total_ordenes_ = total_ordenes_ + 1
   END
END

/**************************     SIMULACION    ***************************************************/
NEWCMD SIMULAR_DIA inventario_inicial cantidad_ordenar punto_reorden bandera_orden dias_orden costo_total total_ordenes_ venta_
   IF punto_reorden > inventario_inicial 'Revisar si hay que reordenar
      REORDENAR bandera_orden dias_orden total_ordenes_ llego_pedido
      IF llego_pedido = 1 'si llego pedido
         llego_pedido = 0 'recibido
         inventario_inicial = inventario_inicial + cantidad_ordenar 'suministre y guarde el resto
      END
   END
   POISSON 1 7 venta_ 'Generar la demanda del día.
   inventario_inicial = inventario_inicial - venta_ 'Disminuir el inventario del dia con la demanda.
END

NEWCMD SIMULAR_PERIODO periodo dias_ inventarios_iniciales_ ordenes_ dias_ordenes_ inventarios_finales_ ventas_ total_costo_inventario_ total_costo_faltantes_ total_costo_ordenes_
   /* Iniciar en el día 1 con la cantidad de “q” unidades.*/
   q = 50 'cantidad por ordenar
   r = 20 'punto de reorden
   i = q 'inventario inicial
   
   bandera_orden = 0
   dias_orden = 0
   total_ordenes = 0
   total_costo_inventario_ = 0
   total_costo_faltantes_ = 0
   dia = 1
   REPEAT periodo 'repetir el proceso por 365 días
      SCORE dia dias_
      SCORE i inventarios_iniciales_
      SIMULAR_DIA i q r bandera_orden dias_orden costo_total total_ordenes venta
      ACUMULAR_COSTO_INVENTARIO total_costo_inventario_ i
      ACUMULAR_COSTO_FALTANTES  total_costo_faltantes_ i
      SCORE venta ventas_
      SCORE bandera_orden ordenes_
      SCORE dias_orden dias_ordenes_
      SCORE i inventarios_finales_
      dia = dia + 1
   END
   CALCULAR_COSTO_ORDEN total_ordenes total_costo_ordenes_
END

/***** MAIN *****/
CLEAROUTPUT
periodo = 365

SIMULAR_PERIODO periodo dia inventario_inicial hay_pedido dias_pedido inventario_final demanda total_costo_inventario total_costo_faltantes total_costo_ordenes
PRINT TABLE dia%4.0F inventario_inicial%16.0F hay_pedido%8.0F dias_pedido%8.0F demanda%8.0F inventario_final%4.0F
PRINT
costo_total = total_costo_inventario + total_costo_faltantes + total_costo_ordenes
PRINT TABLE total_costo_ordenes%16.0F total_costo_inventario%16.0F total_costo_faltantes%16.0F costo_total%16.0F
